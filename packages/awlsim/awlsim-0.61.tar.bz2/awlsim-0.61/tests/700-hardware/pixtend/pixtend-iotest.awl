// Digital input (DI) / output (DO) tests.
// PiXtend DIs are wired to EB 1
// PiXtend DOs are wired to AB 1
FUNCTION FC 1 : VOID
BEGIN
	// Calculate the expected EB 1 from AB 1.
	// The fake PiXtend spidev module wires DI do DO in the following way:
	// EB1 := ((AB1 & 0x3F) | (((AB1 ^ 0x03) & 0x03) << 6))
	L		AB 1
	UW		W#16#3F
	L		AB 1
	XOW		W#16#03
	UW		W#16#03
	SLW		6
	OW
	T		MB 10

	// Compare the actual EB 1 to the expected EB 1 (MB 10)
	L		EB 1
	L		MB 10
	__ASSERT==	__ACCU 1,	__ACCU 2

	// Increment the EB 1 / AB 1 test pattern by one
	L		AB 1
	+		1
	T		AB 1
END_FUNCTION


// GPIO tests
// PiXtend GPIO 0, 1 are wired to A 2.0, A 2.1
// PiXtend GPIO 2, 3 are wired to E 2.0, E 2.1
FUNCTION FC 2 : VOID
BEGIN
	// Calculate the expected E 2.0, E 2.1 from A 2.0, A 2.1
	// The fake PiXtend spidev module wires GPIO 0, 1 to GPIO 2, 3
	L		AB 2
	UW		W#16#03
	T		MB 20

	// Compare the actual EB 1 to the expected EB 1 (MB 20)
	L		EB 2
	L		MB 20
	__ASSERT==	__ACCU 1,	__ACCU 2

	// Increment the EB 2 / AB 2 test pattern by one
	L		AB 2
	+		1
	T		AB 2
END_FUNCTION


// Analog input tests
FUNCTION FC 3 : VOID
BEGIN
	// Check AI0
	// The fake PiXtend spidev module always reports 1 V
	L		EW 10
	L		2754
	__ASSERT==	__ACCU 1,	__ACCU 2

	// Check AI1
	// The fake PiXtend spidev module always reports 2 V
	L		EW 12
	L		5535
	__ASSERT==	__ACCU 1,	__ACCU 2

	// Check AI2
	// The fake PiXtend spidev module always reports 20.1 mA
	L		EW 14
	L		27663
	__ASSERT==	__ACCU 1,	__ACCU 2

	// Check AI3
	// The fake PiXtend spidev module always reports 14.1 mA
	L		EW 16
	L		17295
	__ASSERT==	__ACCU 1,	__ACCU 2
END_FUNCTION


// Analog output tests
FUNCTION FC 4 : VOID
BEGIN
	// Check DAC_A
	// The magic value is checked by the fake PiXtend-Spidev module
	L		27648
	T		AW 10
	
	// Check DAC_B
	// The magic value is checked by the fake PiXtend-Spidev module
	L		2764
	T		AW 12
END_FUNCTION


// PWM output tests
FUNCTION FC 5 : VOID
BEGIN
	// Set PWM0
	// The magic value is checked by the fake PiXtend-Spidev module
	L		W#16#1234
	T		AW 20
	
	// Set PWM1
	// The magic value is checked by the fake PiXtend-Spidev module
	L		W#16#4321
	T		AW 22
END_FUNCTION


ORGANIZATION_BLOCK OB 1
BEGIN
	// This test only works with the fake PiXtend-Spidev module

	CALL		FC 1	// Test DI/DO
	CALL		FC 2	// Test GPIOs
	CALL		FC 3	// Test analog inputs
	CALL		FC 4	// Test analog outputs
	CALL		FC 5	// Test PWMs

	// If we ran 256 test cycles then exit.
	L		MW 100
	+		1
	T		MW 100
	L		256
	<I
	BEB
	CALL		SFC 46 // Exit test; Stop CPU.
END_ORGANIZATION_BLOCK


ORGANIZATION_BLOCK OB 100
BEGIN
	L		0
	T		AB 1
	T		AB 2
	T		MW 100
	CALL		FC 4	// Init analog outputs
	CALL		FC 5	// Init PWMs
END_ORGANIZATION_BLOCK
